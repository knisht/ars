\import ARS
\import Function.Meta
\import Logic
\import Paths
\import Relation
\import Termination

\data StrongConfluence {A : ARS} (a b c : A) (p : a ~> b) (q : a ~> c)
  | strong-join (d : A) (b ~>_= d) (c ~>_* d)

\func isStronglyConfluent (A : ARS) : \Prop =>
  \Pi (a b c : A) (p : a ~> b) (q : a ~> c) -> TruncP (StrongConfluence a b c p q)

\func isChurchRosser (A : ARS) : \Prop =>
  \Pi (a b : A) (a <~>* b) -> Join a b

\func isConfluent (A : ARS) : \Prop =>
  \Pi (a b c : A) (a ~>_* b) (a ~>_* c) -> Join b c

\func isLocallyConfluent (A : ARS) : \Prop =>
  \Pi (a b c : A) (a ~> b) (a ~> c) -> Join b c

\func hasUniqueNormalFormsWrtReduction (A : ARS) : \Prop =>
  \Pi (a b c : A) (a ~>_* b) (a ~>_* c) (isNormalForm b) (isNormalForm c) -> b = c

\func hasUniqueNormalForms (A : ARS) : \Prop =>
  \Pi (a b : A) (a <~>* b) (isNormalForm a) (isNormalForm b) -> a = b

\func hasNormalFormProperty (A : ARS) : \Prop =>
  \Pi (a b : A) (a <~>* b) (isNormalForm b) -> a ~>_* b

\lemma ChurchRosser=>Confluence {A : ARS} (cr : isChurchRosser A) : isConfluent A =>
  \lam a b c a~>*b a~>*c => cr b c (<~>*-compose a (<~>*-reverse (~>*=><~>* a~>*b)) (~>*=><~>* a~>*c))

\lemma Confluence=>ChurchRosser {A : ARS} (conf : isConfluent A) : isChurchRosser A =>
  \lam a b a<~>*b => conf=>rc-helper conf a b a<~>*b
  \where {
    \lemma conf=>rc-helper {A : ARS} (conf : isConfluent A) (a b : A) (a<~>*b : a <~>* b) : Join a b \elim a<~>*b
      | trc-direct p => join a (trc-direct idp) (trc-direct (inv p))
      | trc-connect c a<~>c c<~>*b => \let join-c-b : Join c b => conf=>rc-helper conf c b c<~>*b \in \case join-c-b, a<~>c \with {
        | join c' c~>*c' b~>*c', sc-left a~>c => join c' (trc-connect c a~>c c~>*c') b~>*c'
        | join c' c~>*c' b~>*c', sc-right c~>a => \let join-a-c' : Join a c' => conf c a c' (trc-unary c~>a) c~>*c' \in \case join-a-c' \with {
          | join c'' a~>c'' c'~>c'' => join c'' a~>c'' (~>*-concat b~>*c' c'~>c'')
        }
      }
  }

\lemma ChurchRosser=Confluence {A : ARS} : isChurchRosser A = isConfluent A =>
  propExt ChurchRosser=>Confluence Confluence=>ChurchRosser
